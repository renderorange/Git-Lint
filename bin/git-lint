#!/usr/bin/env perl

use strict;
use warnings;

use FindBin;
use lib "$FindBin::RealBin/../lib";

use Getopt::Long ();
use Pod::Usage   ();
use Git::Lint;

my $VERSION = '0.001';

my %opt = (
    # TODO: until the 'message' check mode is implemented, default to 'commit.'
    # until then, we're silently running check => commit and don't open that
    # option to the user.
    check   => 'commit',
    profile => 'default',
);
Getopt::Long::GetOptions(
    \%opt,
    'profile=s',
    'version' => sub { print "git-lint version $VERSION\n"; exit 0 },
    'help',
) or Pod::Usage::pod2usage( -exitval => 1 );

Pod::Usage::pod2usage( -exitval => 0 ) if ( $opt{help} );

delete $opt{version};
delete $opt{help};

my $lint = Git::Lint->new();
$lint->run(\%opt);

if ( keys %{ $lint->{issues} } ) {
    print STDERR "the following issues were found in your ";

    if ($opt{check} eq 'commit') {
        print STDERR "commit:\n\n";

        foreach my $filename ( keys %{ $lint->{issues} } ) {
            print STDERR $filename . "\n";
            print STDERR $_ . "\n" foreach @{ $lint->{issues}{$filename} };
            print STDERR "\n";
        }
    }
    else {
        print STDERR "message:\n\n";
        print STDERR $_ . "\n" foreach @{ $lint->{issues} };
        print STDERR "\n";
    }

    exit 1;
}

exit 0;

__END__

=pod

=head1 NAME

git-lint - linter for git

=head1 SYNOPSIS

 git-lint [--profile <name>] [--version] [--help]

=head1 DESCRIPTION

C<git-lint> is a program to lint git commits.

=head1 OPTIONS

=over

=item --profile

Run a specific profile of check modules.

Defaults to the 'default' profile.

=item --version

Print the version.

=item --help

Print the help menu.

=back

=head1 AUTHOR

Blaine Motsinger C<blaine@renderorange.com>

=cut
